config {
type: "table",
schema: "reporting",
tags: ["daily", "reporting"],
description: "Combine both questions and answers into a single posts_all table",
columns: {
  title: {
     description: "The title of the post",
     bigqueryPolicyTags: ["projects/doryoku-dacoaching/locations/us/taxonomies/8779509711797272593/policyTags/2836668468478603603"]
   }
},
 bigquery: {
  partitionBy: "date(created_at)"
},
assertions: {
  uniqueKey: ["post_id"],
  nonNull: ["post_id", "created_at"]
}
}
select
post_id,
created_at,
type,
title,
body,
owner_user_id,
parent_id
from
${ref("stg_posts_answers")}
union all
select
post_id,
created_at,
type,
title,
body,
owner_user_id,
parent_id
from
${ref("stg_posts_questions")}
